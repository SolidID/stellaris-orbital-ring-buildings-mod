ring_research_hub = {
	icon = "GFX_orbitalring_research_hub"
	construction_days = 360

	potential = {
		exists = owner
		owner = {
			has_technology = tech_global_research_initiative
		}
		is_orbital_ring = yes
	}

	triggered_planet_modifier = {
		potential = {
			exists = owner
			owner = {
				is_gestalt = no
			}
		}
		modifier = {
			planet_researchers_physics_research_produces_add = 1
			planet_researchers_engineering_research_produces_add = 1
			planet_researchers_society_research_produces_add = 1
			planet_researchers_consumer_goods_upkeep_add = 1
		}
	}

	triggered_planet_modifier = {
		potential = {
			exists = owner
			owner = {
				is_hive_empire = yes
			}
		}
		modifier = {
			planet_researchers_physics_research_produces_add = 1
			planet_researchers_engineering_research_produces_add = 1
			planet_researchers_society_research_produces_add = 1
			planet_researchers_minerals_upkeep_add = 3

		}
	}

	triggered_planet_modifier = {
		potential = {
			exists = owner
			owner = {
				is_machine_empire = yes
			}
		}
		modifier = {
			planet_researchers_physics_research_produces_add = 1
			planet_researchers_engineering_research_produces_add = 1
			planet_researchers_society_research_produces_add = 1
			planet_researchers_energy_upkeep_add = 2
		}
	}

	resources = {
		category = starbase_buildings
		cost = {
			alloys = 200
			exotic_gases = 300
		}

		upkeep = {
			energy = 2
			exotic_gases = 1
		}
	}

	show_in_tech = "tech_global_research_initiative"

	ai_weight = {
		weight = 0
	}
}

ring_refinery_hub = {
	icon = "GFX_orbitalring_refinery_hub"
	construction_days = 360

	potential = {
		exists = owner
		owner = {
			has_technology = tech_rare_crystals
			has_technology = tech_exotic_gases
			has_technology = tech_volatile_motes
		}
		is_orbital_ring = yes
	}

	planet_modifier = {
		planet_refiners_produces_mult = 0.1
		planet_translucers_produces_mult = 0.1
		planet_chemists_produces_mult = 0.1
	}

	resources = {
		category = starbase_buildings
		cost = {
			alloys = 200
			exotic_gases = 100
			rare_crystals = 100
			volatile_motes = 100
		}

		upkeep = {
			energy = 2
			minerals = 20
		}
	}

	ai_weight = {
		weight = 0
	}
}

ring_shield = {
	icon = "GFX_orbitalring_aegis"
	construction_days = 360

	potential = {
		exists = owner
		owner = {
			has_technology = tech_planetary_shield_generator
		}
		is_orbital_ring = yes
	}

	planet_modifier = {
		planet_orbital_bombardment_damage = -0.25
	}

	resources = {
		category = starbase_buildings
		cost = {
			alloys = 400
		}

		upkeep = {
			energy = 50
		}
	}

	show_in_tech = "tech_planetary_shield_generator"

	ai_weight {
	    weight = 0
	}
}

ring_coordination_system = {
	icon = "GFX_orbitalring_bureaucracy_hub"
	construction_days = 360

	potential = {
		exists = owner
		owner = {
			has_technology = tech_adaptive_bureaucracy
			is_machine_empire = yes
			NOT = {
				has_civic = civic_machine_servitor
			}
		}
		is_orbital_ring = yes
	}

	planet_modifier = {
	    custom_tooltip = "ring_coordination_system_effect"
		planet_coordinators_unity_produces_add = 1
	}

	resources = {
		category = starbase_buildings
		cost = {
			alloys = 200
			rare_crystals = 300
		}

		upkeep = {
			energy = 2
			rare_crystals = 1
		}
	}

	show_in_tech = "tech_adaptive_bureaucracy"

	ai_weight = {
		weight = 0
	}
}

organic_convenience_infrastructure = {
	icon = "GFX_building_paradise_dome"
	construction_days = 360

	potential = {
		exists = owner
		owner = {
			has_technology = tech_adaptive_bureaucracy
			is_machine_empire = yes
			has_civic = civic_machine_servitor
		}
		is_orbital_ring = yes
	}

	planet_modifier = {
		planet_bio_trophies_unity_produces_add = 1
	}

	resources = {
		category = starbase_buildings
		cost = {
			alloys = 200
			rare_crystals = 300
		}

		upkeep = {
			energy = 2
			rare_crystals = 1
		}
	}

	show_in_tech = "tech_adaptive_bureaucracy"

	ai_weight = {
		weight = 0
	}
}

ring_maintenance_hub = {
	icon = "GFX_orbitalring_maintenance_hub"
	construction_days = 360

	potential = {
		exists = owner
		owner = {
		    OR = {
		        AND = {
			        has_technology = tech_resource_processing_algorithms
			        is_machine_empire = yes
			    }
			    AND = {
			        is_hive_empire = yes
			        has_technology = tech_hive_cluster
			    }
			}
		}
		is_orbital_ring = yes
	}

	planet_modifier = {
		custom_tooltip = "sm_ring_maintenance_hub_effect"
	}

	resources = {
		category = starbase_buildings
		cost = {
			alloys = 200
			exotic_gases = 300
		}

		upkeep = {
			energy = 2
			exotic_gases = 1
		}
	}

	show_in_tech = "tech_resource_processing_algorithms"
	show_in_tech = "tech_hive_cluster"


	ai_weight = {
		weight = 100
	}
}